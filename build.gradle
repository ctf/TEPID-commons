buildscript {

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$KOTLIN"
    }
}

allprojects {
    group GROUP
    version COMMONS_VERSION
}

subprojects {

    apply plugin: 'java'
    apply plugin: 'kotlin'
    apply plugin: 'maven'

    sourceCompatibility = JVM_TARGET

    repositories {
        mavenCentral()
    }

    compileKotlin {
        kotlinOptions.jvmTarget = JVM_TARGET
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = JVM_TARGET
    }

    test {
        dependsOn cleanTest
        testLogging.showStandardStreams = true
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$KOTLIN"

        testCompile "junit:junit:$JUNIT"
        testCompile "org.jetbrains.kotlin:kotlin-test-junit:$KOTLIN"
    }

}